cmake_minimum_required(VERSION 2.8.2)

project(OpenSSL-download NONE)
include(ExternalProject)

set(EP_SOURCE_DIR ${CMAKE_BINARY_DIR}/openssl-src)
set(EP_BINARY_DIR ${CMAKE_BINARY_DIR}/openssl-src)
set(EP_INSTALL_DIR ${CMAKE_INSTALL_PREFIX})

set(openssl_TAG 1.1.1d)
set(openssl_SHA256 1e3a91bc1f9dfce01af26026f856e064eab4c8ee0a8f457b5ae30b40b8b711f2)
set(openssl_URL "http://www.openssl.org/source/old/1.1.1/openssl-${openssl_TAG}.tar.gz")

include(ProcessorCount)
ProcessorCount(CPU_COUNT)
if(CPU_COUNT EQUAL 0)
  set(CPU_COUNT 2)
endif()

if (WIN32)
    set(OPENSSL_PATCH_COMMAND git apply --ignore-space-change --ignore-whitespace ${CMAKE_SOURCE_DIR}/bcryptgen.patch)
    set(OPENSSL_CONFIG_COMMAND perl configure VC-WIN64A CC=cl RC=rc --prefix=${EP_INSTALL_DIR} --openssldir=${EP_INSTALL_DIR})
    set(OPENSSL_BUILD_COMMAND nmake)
    set(OPENSSL_INSTALL_COMMAND nmake install)
    # OpenSSL's Makefile fails to install the static builds of libcrypto and libssl on Windows
    # These library names are only valid from OpenSSL 1.1 and higher.
    set(OPENSSL_POSTINSTALL_CRYPTO_COMMAND ${CMAKE_COMMAND} -E copy libcrypto_static.lib ${EP_INSTALL_DIR}/lib/libcrypto_static.lib)
    set(OPENSSL_POSTINSTALL_SSL_COMMAND ${CMAKE_COMMAND} -E copy libssl_static.lib ${EP_INSTALL_DIR}/lib/libssl_static.lib)
else()
    set(OPENSSL_PATCH_COMMAND "")
    set(OPENSSL_CONFIG_COMMAND CFLAGS=-fPIC ./config --prefix=${EP_INSTALL_DIR})
    set(OPENSSL_BUILD_COMMAND make -j ${CPU_COUNT})
    if(APPLE)
        set(OPENSSL_INSTALL_COMMAND make install_sw install_ssldirs)
    else()
        set(OPENSSL_INSTALL_COMMAND make install_sw)
    endif()
    set(OPENSSL_POSTINSTALL_CRYPTO_COMMAND "")
    set(OPENSSL_POSTINSTALL_SSL_COMMAND "")
endif()

ExternalProject_Add(openssl
    URL ${openssl_URL}
    URL_HASH SHA256=${openssl_SHA256}
    SOURCE_DIR ${EP_SOURCE_DIR}
    BINARY_DIR ${EP_BINARY_DIR}
    INSTALL_DIR ${EP_INSTALL_DIR}
    PATCH_COMMAND ${OPENSSL_PATCH_COMMAND}
    CONFIGURE_COMMAND ${OPENSSL_CONFIG_COMMAND}
    BUILD_COMMAND ${OPENSSL_BUILD_COMMAND}
    INSTALL_COMMAND ${OPENSSL_INSTALL_COMMAND}
    COMMAND ${OPENSSL_POSTINSTALL_CRYPTO_COMMAND}
    COMMAND ${OPENSSL_POSTINSTALL_SSL_COMMAND}
)

